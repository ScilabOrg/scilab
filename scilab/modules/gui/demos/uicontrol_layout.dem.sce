// Scilab ( http://www.scilab.org/ ) - This file is part of Scilab
// Copyright (C) 2014 - Scilab Enterprises - Bruno JOFRET
//
// This file is released under the 3-clause BSD license. See COPYING-BSD.

f = figure("layout", "border", "position", [0, 0, 800, 800], "visible", "off", "default_axes", "off");

tabPane = uicontrol(f, "style", "tab", ...
"constraints", createConstraints("border", "center"));

// Border Layout
uiBorderPane = uicontrol(tabPane, "style", "frame", ...
"constraints", createConstraints("border", "center"), ...
"layout", "border", ...
"string", "Border Layout");

for i = ["left", "right", "top", "bottom", "center"]
    uicontrol(uiBorderPane, "style", "pushbutton", ...
    "constraints", createConstraints("border", i), ...
    "position", [0, 0, 100, 100], ...
    "string", i);
end

// Gridbag Layout
uiGridBagPane = uicontrol(tabPane, "style", "frame", ...
"constraints", createConstraints("border", "center"), ...
"layout", "gridbag", ...
"string", "GridBag Layout");

for i = 1:5
    for j = 1:5
        uicontrol(uiGridBagPane, "style", "pushbutton", ...
        "constraints", createConstraints("gridbag", [i, j, 1, 1], [1,1], "both"), ...
        "position", [0, 0, 100, 100], ...
        "string", msprintf("Button %dx%d", i, j));
    end
end

// no Layout : absolute positionnning
uiPane = uicontrol(tabPane, "style", "frame", ...
"constraints", createConstraints("border", "center"), ...
"string", "No Layout");

for i = 1:5
    for j = 1:5
        uicontrol(uiPane, "style", "pushbutton", ...
        "position", [10 * i, 10 * j, 100, 100], ...
        "string", msprintf("Button %dx%d", i, j));
    end
end

f.visible = "on";
