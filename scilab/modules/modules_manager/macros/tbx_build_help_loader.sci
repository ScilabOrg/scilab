// Scilab ( http://www.scilab.org/ ) - This file is part of Scilab
// Copyright (C) 2008 - INRIA - Simon LIPP <simon.lipp@scilab.org>
// Copyright (C) 2010 - DIGITEO - Pierre MARECHAL
//
// Copyright (C) 2012 - 2016 - Scilab Enterprises
//
// This file is hereby licensed under the terms of the GNU GPL v2.0,
// pursuant to article 5.3.4 of the CeCILL v.2.1.
// This file was originally licensed under the terms of the CeCILL v2.1,
// and continues to be available under such terms.
// For more information, see the COPYING file which you should have received
// along with this program.

// Generate a loader file for the help

function tbx_build_help_loader(moduletitle,path)

    rhs = argn(2);

    // Number of input arguments

    if and(rhs<> [1 2]) then
        error(msprintf(gettext("%s: Wrong number of input arguments: %d to %d expected.\n"),"tbx_build_help_loader",1,2));
    end

    // Input argument N°1
    // → toolbox_title

    if type(moduletitle) <> 10 then
        error(msprintf(gettext("%s: Wrong type for input argument #%d: string expected.\n"),"tbx_build_help_loader",1));
    end

    if size(moduletitle,"*") <> 1 then
        error(msprintf(gettext("%s: Wrong size for input argument #%d: string expected.\n"),"tbx_build_help_loader",1));
    end

    // Input argument N°2
    // → path

    if rhs<2 then
        path = pwd();

    else
        if type(path) <> 10 then
            error(msprintf(gettext("%s: Wrong type for input argument #%d: string expected.\n"),"tbx_build_help_loader",1));
        end

        if size(path,"*") <> 1 then
            error(msprintf(gettext("%s: Wrong size for input argument #%d: string expected.\n"),"tbx_build_help_loader",1));
        end

        if ~isdir(path) then
            error(msprintf(gettext("%s: The directory ''%s'' doesn''t exist or is not read accessible.\n"),"tbx_build_help_loader",path));
        end
    end

    // build the loader file

    loadertxt = [
    "// This file is released under the 3-clause BSD license. See COPYING-BSD.",
    "// Generated by tbx_build_help_loader: Please, do not edit this file",
    "",
    "add_help_chapter("""+toolbox_title+""",get_absolute_file_path(""addchapter.sce""),%F);",
    "clear add_help_chapter;"];

    loaderfile = pathconvert(path+"/addchapter.sce",%F);

    if ~mputl(loadertxt,loaderfile) then
        error(msprintf(gettext("%s: The file %s cannot been created, please check if you have write access on this file.\n"),"tbx_build_help_loader",loaderfile));
    end

endfunction
