// =============================================================================
// Scilab ( http://www.scilab.org/ ) - This file is part of Scilab
// Copyright (C) 2015 - Scilab Enterprises - Paul Bignier
//
//  This file is distributed under the same license as the Scilab package.
// =============================================================================
//
// <-- XCOS TEST -->
// Load and run a diagram that calls a Scilab macro (tkscaleblk.sci)
exec("SCI/modules/scicos/tests/unit_tests/bug_8348.cosf", -1);
// Call sciblk2 instead of sciblk4
scs_m.objs(1).model.sim = list("tkscaleblk", 3);
scs_m.props.tf = 100;
// Rewrite tkscaleblk so it minds sciblk2's calling sequence
wMode = warning("query");
warning("off")
function [xd, tvec, z, x, outptr] = tkscaleblk(flag, nevprt, t, x, z, rpar, ipar, inptr)
    outptr = list();
    if flag == 1 then
        // Output update
        slider = get("-38f07e57:12bd41b596e:-7f2b#slider");
        if slider <> [] then
            // Calculate real value
            value = get(slider,"value") / rpar(3);
            w = get("-38f07e57:12bd41b596e:-7f2b");
            if w <> [] then
                set(w, "info_message", string(value));
            end
            outptr = list(value);
        end
    elseif flag == 4 then
        // Initialization
        // If already exists (stopped) then reuse
        f = get("-38f07e57:12bd41b596e:-7f2b");
        if f <> [] then
            return;
        end
        f = figure("Figure_name", "TK Source: " + "", ...
        "dockable", "off", ...
        "infobar_visible" , "on", ...
        "toolbar", "none", ...
        "menubar_visible", "off", ...
        "menubar", "none", ...
        "backgroundcolor", [1 1 1], ...
        "default_axes", "off", ...
        "figure_size", [180 350], ...
        "layout", "border", ...
        "figure_position", [40 40], ...
        "Tag", "-38f07e57:12bd41b596e:-7f2b");
        frame_slider = uicontrol(f, ...
        "style", "frame", ...
        "constraints", createConstraints("border", "left", [180, 0]), ...
        "border", createBorder("line", "lightGray", 1), ...
        "backgroundcolor", [1 1 1], ...
        "layout", "gridbag");
        // Slider
        bounds = rpar(1:2);
        initial = mean(bounds);
        uicontrol(frame_slider, ...
        "Style", "slider", ...
        "Tag", "-38f07e57:12bd41b596e:-7f2b#slider", ...
        "Min", bounds(1), ...
        "Max", bounds(2), ...
        "Value", initial, ...
        "Position", [0 0 10 20], ...
        "SliderStep", [rpar(3) 2*rpar(3)]);
        frame_label = uicontrol(frame_slider, ...
        "style", "frame", ...
        "constraints", createConstraints("border", "right"), ...
        "backgroundcolor", [1 1 1], ...
        "layout", "gridbag");
        // Labels
        labels = string([bounds(2) ; ...
        mean([bounds(2) initial])  ; ...
        initial                    ; ...
        mean([bounds(1) initial])  ; ...
        bounds(1)]);
        labels = "<html>" + strcat(labels, "<br /><br /><br />") + "</html>";
        uicontrol(frame_label, ...
        "Style", "text", ...
        "String", labels(1), ...
        "FontWeight", "bold", ...
        "backgroundcolor", [1 1 1]);
        // Update default value
        outptr = list(initial / rpar(3));
    elseif flag == 5 then
        // Ending
        f = get("-38f07e57:12bd41b596e:-7f2b");
        if f <> [] then
            close(f);
        end
    end
    xd = 0;
    tvec = [];
endfunction
warning(wMode);
cpr = scicos_simulate(scs_m);
